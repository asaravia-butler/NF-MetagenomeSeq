//******** Global parameters *****************//
params {

    technology  = "nanopore" // ['illumina', 'nanopore']
    sample_type = "low_biomass" // ['standard', 'low_biomass']
    // Only relavant when technology is "nanopore"
    input_type = 'single' //['single', 'multiple', 'directory']

    // Input file
    // Sample metdata file with sample names matching file paths and sample metadata
    input_file = null 
    // A directdory containing input raw fast5 or pod5 files to demultiplex 
    input_dir = "/path/to/directory/containing/fast5/or/pod5/files"
    isFast5 = false // [true or false]
    /* Run assembly-based workflow, read-based, or both
      (values need to be one of: "assembly-based", "read-based", or "both")
      It runs both by default
    */
    workflow     = "both"

    assay_suffix = "_GLmetagenomics"

    // Additional prefix to add to output files that describe more than one sample (to make them unique compared to other datasets) 
    // leave as empty, i.e. "", if not wanted, include separator at end if adding one, e.g. "Swift1S_"
    additional_filename_prefix = "" 

    publishDir_mode = "link" // "copy", "link", "symlink"	
   
    // Quality trimmed/filtered suffixes
    filtered_R1_suffix = "_R1_filtered.fastq.gz"
    filtered_R2_suffix = "_R2_filtered.fastq.gz"
    filtered_suffix    = "_filtered.fastq.gz"  // If single-end

    //-------------------------- Directories ----------------------------------//

    // Raw reads directory (can be relative to workflow directory, or needs to be full path)
    raw_reads_dir           = "../Raw_Sequence_Data/"
    // Output directories (all relative to processing directory, will be created)
    fastqc_out_dir          = "../FastQC_Outputs/"
    filtered_reads_dir      = "../Filtered_Sequence_Data/"
    noblank_reads_dir       = "../Blank_removed/"
    nohost_reads_dir        = "../Host_removed/"
    assembly_based_dir      = "../Assembly-based_Processing/"
    assemblies_dir          = "../Assembly-based_Processing/assemblies/"
    genes_dir               = "../Assembly-based_Processing/predicted-genes/"
    annotations_and_tax_dir = "../Assembly-based_Processing/annotations-and-taxonomy/"
    mapping_dir             = "../Assembly-based_Processing/read-mapping/"
    combined_output_dir     = "../Assembly-based_Processing/combined-outputs/"
    bins_dir                = "../Assembly-based_Processing/bins/"
    MAGs_dir                = "../Assembly-based_Processing/MAGs/"
    read_based_dir          = "../Read-based_Processing/"
    genelab_dir             = "../GeneLab/"
    logs_dir                = "../Logs/"
    metadata_dir            = "../Metadata/" 
    genome_mapping_dir      = "../Read-based_Processing/genome_mapping/"

    //************************* Databases **********************************//
    CAT_DB_LINK      = "https://tbb.bio.uu.nl/bastiaan/CAT_prepare/CAT_prepare_20210107.tar.gz"
    GTDBTK_LINK      = "https://data.gtdb.ecogenomic.org/releases/release220/220.0/auxillary_files/gtdbtk_package/full_package/gtdbtk_r220_data.tar.gz"
    cat_db           = "/global/data/temp_scratch/oobayomi/metagenomics/version3/processing/Reference_DBs/CAT_prepare_20210107/"
    ko_db_dir        = "/global/data/temp_scratch/oobayomi/metagenomics/version3/processing/Reference_DBs/kofamscan_db/"
    metaphlan_db_dir = "/global/data/temp_scratch/oobayomi/metagenomics/version3/processing/Reference_DBs/metaphlan4-db/"
    chocophlan_dir   = "/global/data/temp_scratch/oobayomi/metagenomics/version3/processing/Reference_DBs/humann3-db/chocophlan/"
    uniref_dir       = "/global/data/temp_scratch/oobayomi/metagenomics/version3/processing/Reference_DBs/humann3-db/uniref/"
    utilities_dir    = "/global/data/temp_scratch/oobayomi/metagenomics/version3/processing/Reference_DBs/humann3-db/utility_mapping/"
    gtdbtk_db_dir    = "/global/data/temp_scratch/oobayomi/metagenomics/version3/processing/Reference_DBs/GTDB-tk-ref-db/"
    host_db_dir      = "/global/data/Data_Processing/Metagenomics_Datasets/Reference_DBs/kraken2-human-db/"
    krakendb_url     = "https://genome-idx.s3.amazonaws.com/kraken/k2_pluspfp_20250714.tar.gz"
    krakendb_dir     = "/global/data/temp_scratch/oobayomi/low_biomass/databases/kraken2/"
    kaijudb_name     = "nr_euk" // nr, nr_euk, refseq, refseq_nr, refseq_ref, progenomes, fungi, viruses, plasmids, rvdb
    kaijudb_dir      = "/global/data/temp_scratch/oobayomi/low_biomass/databases/kaiju/"
    custome_genome   = "/global/data/temp_scratch/oobayomi/low_biomass/nanopore/pc_mapping_test/genomes/jpl/genomes_plus_lambda.fna"
    
    // Quality assessment parameters
    swift_1S       = false
    adapters       = "${projectDir}/config/bbtools_adapters.fa"
    multiqc_config = "${projectDir}/config/multiqc.config"


    //----------- Host removal parameters
    // build database using the supplied host name
    host_name  = null // "human"
    // Download tar.gz file of database from the supplied url
    // Prebuilt kraken databases can be found here https://benlangmead.github.io/aws-indexes/k2
    host_url   = null // "https://zenodo.org/records/8339700/files/k2_Human_20230629.tar.gz?download=1"
    // Build a custome database using the supplied sequence(s) 
    host_fasta = null // "/path/to/host_sequences.fasta" 

    // Assembly parameter
    max_mem        = 100e9 // 100GB

    // Binning parameter
    reduced_tree   = "True"

    // Annotation parameters
    pileup_mem     = "5g" // pileup.sh paramater for calculating contig coverage and depth
    block_size     = 4  // CAT blocksize

    //********************  CAT database directory strings ************************//
    // The strings below will be added to the end of the "cat_db" parameter provided above
    // cat taxonomy directory with cat_db path provided above
    cat_taxonomy_dir = "2021-01-07_taxonomy/"
    cat_db_sub_dir   = "2021-01-07_CAT_database/"

    // MAG parameters
    min_est_comp       = 90  // Minimum estimated completeness
    max_est_redund     = 10  // Maximum estimated redundancy
    max_est_strain_het = 50  // Maximum estimated strain heterogeneity

    /*
    Scratch directory for gtdb-tk, if wanting to use disk space instead of RAM, can be memory intensive;
    see https://ecogenomics.github.io/GTDBTk/faq.html#gtdb-tk-reaches-the-memory-limit-pplacer-crashes
    leave empty if wanting to use memory, the default, put in quotes the path to a directory that 
    already exists if wanting to use disk space
    */

    use_gtdbtk_scratch_location = false


    // Specify paths to existing conda environments
    conda_genelab         = null          // "/path/to/envs/genelab-utils"
    conda_qc              = null          // "/path/to/envs/qc"
    conda_humann3         = null          // "/path/to/envs/humann3"
    conda_cat             = null          // "/path/to/envs/genelab-utils/envs/CAT"
    conda_prodigal        = null          // "/path/to/envs/prodigal"
    conda_metabat         = null          // "/path/to/envs/metabat"
    conda_gtdbtk          = null          // "/path/to/envs/gtdbtk"
    conda_kegg_decoder    = null          // "/path/to/envs/kegg_decoder"
    conda_megahit         = null          // "/path/to/envs/megahit"
    conda_bit             = null          // "/path/to/envs/bit"
    conda_kofamscan       = null          // "/path/to/envs/kofamscan"
    conda_mapping         = null          // "/path/to/envs/mapping"
    conda_checkm          = null          // "/path/to/envs/checkm"
    conda_rgi             = null          // "/path/to/envs/rgi"    
    conda_kraken2         = null          // "/path/to/envs/kraken2"
    conda_kaiju           = null          // "/path/to/envs/kaiju"
    conda_krona           = null          // "/path/to/envs/krona"
    conda_pavian          = null          // "/path/to/envs/pavian"
    conda_nanoplot        = null          // "/path/to/envs/nanoplot"
    conda_krakentools     = null          // "/path/to/envs/krakentools"
    conda_filtlong        = null          // "/path/to/envs/filtlong"
    conda_porechop        = null          // "/path/to/envs/porechop"
    conda_samtools        = null          // "/path/to/envs/samtools"
    conda_dorado          = null          // "/path/to/envs/dorado"
    conda_pod5            = null          // "/path/to/envs/pod5"
    conda_spades          = null          // "/path/to/envs/spades"
    conda_fastp           = null          // "/path/to/envs/fastp"
    conda_flye            = null          // "/path/to/envs/flye"
    conda_medaka          = null          // "/path/to/envs/medaka"

    accession          = null // GLDS or OSD acession number for the data to be processed
    // Pattern of files on OSDR for the GLDS_accession you want to process.
    RawFilePattern     = null // "_metaG", "_HRremoved"
    errorStrategy      = "terminate" // nextflow's error handling strategy
    debug              = false // should info about the parameters set by the user be shown when the workflow starts.
}

// Setting the default container engine to singularity
params.containerEngine = "singularity"
// Conda shouldn't be used by default except when using conda-based profiles
params.use_conda = false



/*******************************************************************************************************
*************************************** Workflow Profiles **********************************************
********************************************************************************************************/

profiles {

    slurm {
         process.executor     = 'slurm' 
     }

    conda {   
        conda.enabled          = true
        params.use_conda       = true
        conda.channels         = 'conda-forge,bioconda' 
        conda.cacheDir         = 'conda/' // location of conda environments
        conda.createTimeout    = '2h'              
    }

    mamba {
        conda.enabled          = true
        conda.useMamba         = true
        conda.channels         = 'conda-forge,bioconda'
        params.use_conda       = true
        conda.cacheDir         = 'conda/' // location of conda environments
        conda.createTimeout    = '2h'
    }

    singularity {
        singularity.enabled    = true
        singularity.autoMounts = true
        //singularity.cacheDir   = "singularity/" // location of singularity images
        params.containerEngine = "singularity"
    }

    docker {
        docker.enabled         = true
        docker.runOptions      = '-u $(id -u):$(id -g)'
        params.containerEngine = "docker"
    }

}

// Maximum number of jobs to submit in parallel
executor.queueSize = 30

/* 
    Root directory where the databases will be downloaded if they don't exist.
    This should be provided as a full path (starting with '/').
    Note that relative paths such as '~/' and '../' are not expanded
    by nextflow's evaluation of files, so don't use thosee.
*/ 
params.DB_ROOT = "${launchDir.getParent()}/Reference_DBs"

// Mount Humann databases to their predefined locations in the Biobakery container being used
if(params.chocophlan_dir == null || params.uniref_dir == null || params.metaphlan_db_dir == null || params.utilities_dir == null) {

    utilities = "${params.DB_ROOT}/humann3-db/utility_mapping/:/usr/local/lib/python3.6/dist-packages/humann/data/misc"

}else{

    utilities = "${params.utilities_dir}:/usr/local/lib/python3.6/dist-packages/humann/data/misc"
}


/******************************************************************************************************************
***************** Tune process specific resources (cpu, container, memory etc.) ***********************************
*******************************************************************************************************************/

process {

    //******************* Default process settings ************************//
    errorStrategy = { params.errorStrategy  ? params.errorStrategy : "ignore"}
    queue = "normal,priority"
    maxRetries = 2
    memory = '5 GB'
    cache = 'lenient'
    cpus = 8

    /*********************************************************************************************
    ******************************** Process Specific Settings  **********************************
    *********************************************************************************************/
    
    //************************* Generic process labels used throughout the workflow  ****************// 

    withLabel: genelab {
              
                  conda = {params.conda_genelab ? params.conda_genelab : "${projectDir}/envs/genelab.yaml"}
                  container = "olabiyi/genelab-utils:1.3.22"
            }


    withLabel: bit {
                  cpus = 2
                  conda = {params.conda_bit ? params.conda_bit : "${projectDir}/envs/bit.yaml"}
                  container = "olabiyi/bit-astrobiomike:1.0"
                  memory = "5 GB"
            }

    withLabel: samtools {

                  conda = {params.conda_samtools ? params.conda_samtools : "${projectDir}/envs/samtools.yaml"}
                  container = "quay.io/biocontainers/samtools:1.22.1--h96c455f_0"
    }

//*************************************** Database set-up ********************************************//
    withLabel: humann_setup {
                  conda = {params.conda_humann3 ? params.conda_humann3 : "${projectDir}/envs/humann3.yaml"}
                  container = "biobakery/humann:3.9"
            }

    withName: SETUP_METAPHLAN {
                  memory = "100 GB"
            }


    withName: SETUP_CAT_DB {
                  conda = {params.conda_cat ? params.conda_cat : "${projectDir}/envs/cat.yaml"}
                  container = "olabiyi/bit-astrobiomike:1.0"
            }
    

    withLabel: db_setup {
                  storeDir = "${params.DB_ROOT}/"
            }

    withName: SETUP_CAT_DB {
                  conda = {params.conda_cat ? params.conda_cat : "${projectDir}/envs/cat.yaml"}
                  container = "olabiyi/bit-astrobiomike:1.0"
            }

    withName: SETUP_KOFAMSCAN_DB {
                  conda = {params.conda_kofamscan ? params.conda_kofamscan : "${projectDir}/envs/kofamscan.yaml"}
                  container = "olabiyi/bit-astrobiomike:1.0"
            }

    withName: SETUP_GTDBTK_DB {
                  conda = {params.conda_gtdbtk ? params.conda_gtdbtk : "${projectDir}/envs/gtdb-tk.yaml"}
                  container =  "quay.io/biocontainers/gtdbtk:2.4.0--pyhdfd78af_1"
            }

//************************* GLDS_accession runsheet and input file retrieval  **************************************//
    withName: GET_RUNSHEET {
                  cpus = 10
                  conda = {params.conda_genelab ? params.conda_genelab : "${projectDir}/envs/genelab.yaml"}
                  container = "olabiyi/genelab-utils:1.3.22"
                  publishDir = [path: params.genelab_dir , mode: params.publishDir_mode]
            }

//*********************************** Base calling and demultiplexing ******************************************//

    withLabel: dorado {
                  cpus = 10
                  conda = {params.conda_dorado ? params.conda_dorado : "${projectDir}/envs/dorado.yaml"}
                  container = "quay.io/staphb/dorado:0.9.0-cuda12.2.0"
            }

    withName: FAST52POD5 {
                  cpus = 10
                  conda = {params.conda_pod5 ? params.conda_pod5 : "${projectDir}/envs/pod5.yaml"}
                  container = "quay.io/biocontainers/pod5:0.3.15--pyhdfd78af_0"
            }


//********************************** Read quality control and assesment ********************************************//
    
     withLabel: quality_check {
                  cpus = 4
                  memory = '40 GB'
                  publishDir = [path: params.raw_reads_dir, mode: params.publishDir_mode]
            }

     withName: FASTQC {
                  conda = {params.conda_qc ? params.conda_qc : "${projectDir}/envs/qc.yaml"}
                  container = "staphb/fastqc:0.12.1"
            }


    withName: NANOPLOT {

                  conda = {params.conda_nanoplot ? params.conda_nanoplot : "${projectDir}/envs/nanoplot.yaml"}
                  container = "quay.io/biocontainers/nanoplot:1.44.1--pyhdfd78af_0"
            }

    withName: MULTIQC {
                  conda = {params.conda_qc ? params.conda_qc: "${projectDir}/envs/qc.yaml"}
                  container = "staphb/multiqc:1.19"
                  cpus = 2
                  memory = '100 GB'
                  publishDir = [path: params.fastqc_out_dir, mode: params.publishDir_mode]
          }

    withLabel:  bbtools {
                  conda = {params.conda_qc ? params.conda_qc: "${projectDir}/envs/qc.yaml"}
                  container = "staphb/bbtools:38.86"
                  cpus = 5
                  memory = "60 GB"
         }


    withName: BBDUK {
                  publishDir = [[path: params.filtered_reads_dir, pattern: "*${params.filtered_suffix}" , mode: params.publishDir_mode],
                                [path: params.logs_dir, pattern: "*-bbduk.log" , mode: params.publishDir_mode]]
            }

   withLabel: fastp {
 
        conda = {params.conda_fastp ?  params.conda_fastp : "${projectDir}/envs/fastp.yaml"}
        container = "quay.io/biocontainers/fastp:0.24.0--heae3180_1"
        cpus = 5
        memory = "60 GB"
        publishDir = [[path: params.filtered_reads_dir, pattern: "*${params.filtered_suffix}" , mode: params.publishDir_mode],
                      [path: params.logs_dir, pattern: "*-fastp.log" , mode: params.publishDir_mode]]

   }

   withName: FILTLONG {

                  conda = {params.conda_filtlong ? params.conda_filtlong: "${projectDir}/envs/filtlong.yaml"}
                  container = 'quay.io/biocontainers/filtlong:0.2.1--hdcf5f25_4'
                  publishDir = [[path: params.filtered_reads_dir, pattern: "*${params.filtered_suffix}" , mode: params.publishDir_mode],
                                [path: params.logs_dir, pattern: "*-filtlong.log" , mode: params.publishDir_mode]]
            }

  withName: PORECHOP {

                  conda = {params.conda_porechop ? params.conda_porechop: "${projectDir}/envs/porechop.yaml"}
                  container = 'quay.io/biocontainers/porechop:0.2.4--py311he264feb_9'
                  publishDir = [[path: params.filtered_reads_dir, pattern: "*_trimmed.fastq.gz" , mode: params.publishDir_mode],
                                [path: params.logs_dir, pattern: "*-porechop.log" , mode: params.publishDir_mode]]
            }

//************************************** Remove contaminant ****************************************//

    withName: ASSEMBLE_CONTAMINANT {
                  conda = {params.conda_megahit ? params.conda_megahit : "${projectDir}/envs/megahit.yaml"}
                  container = "biocontainers/megahit:1.2.9_cv1"
                  cpus = 8
                  memory = "20 GB"
                  publishDir = [path: params.noblank_reads_dir, pattern: "*-assembly.log", mode: params.publishDir_mode]
            }


   withName: SPADES {

                  conda     = {params.conda_spades ? params.conda_spades : "${projectDir}/envs/spades.yaml"}
                  container = 'quay.io/biocontainers/spades:4.1.0--haf24da9_0'
                  cpus = 8
                  memory = "20 GB"
                  publishDir = [path: params.noblank_reads_dir, pattern: "*-assembly.log", mode: params.publishDir_mode]

      
  }

   withLabel: remove_contaminant  {
                  conda = {params.conda_mapping ? params.conda_mapping : "${projectDir}/envs/mapping.yaml"}
                  container = "biocontainers/bowtie2:v2.4.1_cv1"
                  cpus = 8
                  memory = "20 GB" // {20.GB * task.attempt}
               }

    withName: REMOVE_CONTAMINANT {

                  publishDir = [path: params.noblank_reads_dir, pattern: "*_blank_removed.fastq.gz", mode: params.publishDir_mode]
    }


    withName: "BUILD_CONTAMINANT_INDEX|MAPPING_TO_CONTAMINANT" {
                  container =  "quay.io/biocontainers/minimap2:2.28--h577a1d6_4"
            }


//***************************************** Remove Host ***************************************************//

    withLabel: kraken2 {

          conda = {params.conda_kraken2 ? params.conda_kraken2 : "${projectDir}/envs/kraken2.yaml"}
          container = "quay.io/biocontainers/kraken2:2.1.6--pl5321h077b44d_0"

          }

    withName: REMOVE_HOST {

          publishDir = [path: params.nohost_reads_dir, mode: params.publishDir_mode]

          }



//************************************ Read-based processing *********************************************************//

  withLabel: kaiju {

            conda = {params.conda_kaiju ? params.conda_kaiju :"${projectDir}/envs/kaiju.yaml"}
            container =  'quay.io/biocontainers/kaiju:1.10.1--h5ca1c30_2'

    }

   withName: "KRAKEN_CLASSIFY|KAIJU_CLASSIFY" {

            memory = "300 GB"
   }

   withName: KRAKEN2TABLE {

             conda = {params.conda_pavian ? params.conda_pavian :"${projectDir}/envs/pavian.yaml"}
             container =  'quay.io/nasa_genelab/r-pavian:1.2.0'

    }


   withName: KRAKEN2KRONA {

             conda = {params.conda_krakentools ? params.conda_krakentools :"${projectDir}/envs/krakentools.yaml"}
             container =  'quay.io/biocontainers/krakentools:1.2--pyh7e72e81_1'


   }


   withName: "NODECONTAM_MAP2GENOME|FILTERED_MAP2GENOME|DECONTAMED_MAP2GENOME" {

       publishDir = [path: params.genome_mapping_dir, mode: params.publishDir_mode]

   }
  
   withLabel: read_based {
              conda = {params.conda_humann3 ? params.conda_humann3 : "${projectDir}/envs/humann3.yaml"}
              container = "biobakery/humann:3.9"
           }
     

   withLabel: read_based_outputs {
              publishDir = [path: params.read_based_dir, mode: params.publishDir_mode]
           }

    withName: RGI {

                  conda = {params.conda_rgi ? params.conda_rgi : "${projectDir}/envs/rgi.yaml"}
                  container = "quay.io/biocontainers/rgi:6.0.3--pyha8f3691_0"
                  cpus = 8
                  memory = "60 GB"

    }

    withLabel: krona {

                  conda = {params.conda_krona ? params.conda_krona : "${projectDir}/envs/krona.yaml"}
                  container = "quay.io/biocontainers/krona:2.8.1--pl5321hdfd78af_1"

    }


    withName: HUMANN {
               cpus = 8
               memory = "150 GB"
            }

    withName: GEN_READ_BASED_PROCESSING_KO_TABLE {
               containerOptions = { params.containerEngine == "singularity" ? "-B ${utilities}" : "-v ${utilities}"}
            }


//*************************************** Assembly-based proessing **************************************************//

    withLabel: assembly {
               publishDir = [path: params.assemblies_dir, mode: params.publishDir_mode]
              }

    withName: ASSEMBLE {
                  conda = {params.conda_megahit ? params.conda_megahit : "${projectDir}/envs/megahit.yaml"}
                  container = "biocontainers/megahit:1.2.9_cv1"
                  cpus = 8
                  memory = "20 GB"
                  publishDir = [path: params.logs_dir, pattern: "*-assembly.log", mode: params.publishDir_mode]
            }

    withLabel: flye {
                  conda = {params.conda_flye ? params.conda_flye : "${projectDir}/envs/flye.yaml"}
                  container = "quay.io/biocontainers/flye:2.9.5--py310h275bdba_2"
                  cpus = 8
                  memory = "20 GB"
            }

    withName: POLISH_ASSEMBLY {
                  conda = {params.conda_medaka ? params.conda_medaka : "${projectDir}/envs/medaka.yaml"}
                  container = "quay.io/biocontainers/medaka:2.1.1--py310h5713b5a_0"
                  cpus = 8
                  memory = "20 GB"
                  publishDir = [path: params.logs_dir, pattern: "*-assembly.log", mode: params.publishDir_mode]
            }

    withName: RENAME_HEADERS{

              publishDir = [path: params.assemblies_dir, pattern: "*-assembly.fasta" , mode: params.publishDir_mode]

            }


    withLabel: mapping {
                  conda = {params.conda_mapping ? params.conda_mapping : "${projectDir}/envs/mapping.yaml"}
                  cpus = 8
                  //errorStrategy = 'retry'
                  //maxRetries = 2
                  memory = "20 GB" // {20.GB * task.attempt}
            }

    withName: MAPPING {
                  container =  "quay.io/biocontainers/minimap2:2.28--h577a1d6_4" // "biocontainers/bowtie2:v2.4.1_cv1" //
                  publishDir = [path: params.mapping_dir, pattern: "*-mapping-info.txt", mode: params.publishDir_mode]
            }

    withName: SAM_TO_BAM {
                  container = "staphb/samtools:1.20"
                  publishDir = [path: params.mapping_dir, mode: params.publishDir_mode]
            }

    withName: CALL_GENES {
                  conda = {params.conda_prodigal ? params.conda_prodigal : "${projectDir}/envs/prodigal.yaml"}
                  container = "quay.io/biocontainers/prodigal:2.6.3--h031d066_8"
                  cpus = 8
                  publishDir = [path: params.genes_dir, pattern: "*-genes.gff", mode: params.publishDir_mode]
            }
    
    withLabel: call_genes {
               publishDir = [path: params.genes_dir, mode: params.publishDir_mode]
            }

    withLabel: contig_annotation {
               publishDir = [path: params.annotations_and_tax_dir, mode: params.publishDir_mode]
            }

    withName: KO_ANNOTATION {
                  conda = {params.conda_kofamscan ? params.conda_kofamscan : "${projectDir}/envs/kofamscan.yaml"}
                  container = "quay.io/biocontainers/kofamscan:1.3.0--hdfd78af_2"
                  cpus = 8
                  memory = "10 GB"
                  disk = "20 GB"
            }

    withName: TAX_CLASSIFICATION {
                  conda = {params.conda_cat ? params.conda_cat : "${projectDir}/envs/cat.yaml"}
                  container = "nanozoo/catbat:5.2.3--e9c0a44" 
                  cpus = 8
                  memory = "50 GB"
                  disk = "100 GB"
            }

    withName: GET_COV_AND_DET {
                  conda = {params.conda_mapping ? params.conda_mapping : "${projectDir}/envs/mapping.yaml"}
                  container = "staphb/bbtools:38.86"
                  cpus = 8
                  memory = "40 GB"
                  publishDir = [path: params.mapping_dir, mode: params.publishDir_mode]
            }

    withLabel: combine_outputs {
                  publishDir = [path: params.combined_output_dir, mode: params.publishDir_mode]
            }


    withName: METABAT_BINNING {
                  conda = {params.conda_metabat ? params.conda_metabat : "${projectDir}/envs/metabat.yaml"}
                  container = "nanozoo/metabat2:2.15--c1941c7"
                  cpus = 8
                  publishDir = [path: params.mapping_dir, mode: params.publishDir_mode, pattern: "*-metabat-assembly-depth.tsv"]
            }

    withLabel: bins {
            publishDir = [path: params.bins_dir, mode: params.publishDir_mode]
            } 

    withName: ZIP_BINS {
              publishDir = [path: params.bins_dir, mode: params.publishDir_mode]
            }

     withName: "CHECKM_ON_BIN|COMBINE_CHECKM" {
                  conda = {params.conda_checkm ? params.conda_checkm : "${projectDir}/envs/checkm.yaml"}
                  container = "nanozoo/checkm:1.1.3--c79a047"
                  cpus = 8
                  memory = "50 GB"
                  disk = "50 GB"
            }

    withLabel: mags {
            publishDir = [path: params.MAGs_dir, mode: params.publishDir_mode]
            }

    withName: ZIP_MAGS {
              publishDir = [path: params.MAGs_dir, mode: params.publishDir_mode]
            }


    withName: GTDBTK_ON_MAGS {
                  conda = {params.conda_gtdbtk ? params.conda_gtdbtk : "${projectDir}/envs/gtdb-tk.yaml"}
                  container =  "quay.io/biocontainers/gtdbtk:2.4.0--pyhdfd78af_1"
                  containerOptions = { params.containerEngine == "singularity" ? "-B \${PWD}:/data -B ${gtdbtk_db_dir}:/refdata" : "-v \${PWD}:/data -v ${gtdbtk_db_dir}:/refdata" } 
                  cpus = 8
                  memory = "600 GB"
                  disk = "200 GB"
            }

    withName:  SUMMARIZE_MAG_KO_ANNOTS_WITH_KEGG_DECODER {
                  conda = {params.conda_kegg_decoder ? params.conda_kegg_decoder : "${projectDir}/envs/keggdecoder.yaml"}
                  container = "fmalmeida/keggdecoder:latest"
                  cpus = 8
            }

    withName:  GENERATE_ASSEMBLY_PROCESSING_OVERVIEW_TABLE {
                 publishDir = [path: params.assembly_based_dir, mode: params.publishDir_mode]
            }

}


/*****************************************************************************
********************** Workflow Resource Usage Capturing *********************
******************************************************************************/

// Adapted from : https://github.com/nf-core/rnaseq/blob/master/nextflow.config
def trace_timestamp = new java.util.Date().format( 'yyyy-MM-dd_HH-mm-ss')
timeline {
    enabled = true
    file    = "../Resource_Usage/execution_timeline_${trace_timestamp}.html"
}
report {
    enabled = true
    file    = "../Resource_Usage/execution_report_${trace_timestamp}.html"
}
trace {
    enabled = true
    file    = "../Resource_Usage/execution_trace_${trace_timestamp}.txt"
}



/******************************************************************************
**************************** Workflow Metadata ********************************
*******************************************************************************/

manifest {
    author = 'Olabiyi Aderemi Obayomi, Mike Douglas Lee'
    homePage = 'https://github.com/nasa/GeneLab_Data_Processing/blob/master/Metagenomics/'
    description = 'Metagenomics workflow for pipeline document GL-DPPD-7107-A'
    mainScript = 'main.nf'
    defaultBranch = 'main'
    nextflowVersion = '>=24.04.4'
    version = '1.0.0'
}
